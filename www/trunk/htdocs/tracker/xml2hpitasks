#!/usr/bin/perl

use strict;
use XML::Simple;
use Data::Dumper;

my @RELEASES = ("0.3","0.4","0.5","0.6","0.7","0.8","0.9","1.0","Future","None");

my $file = shift;

my $features = {};
my $bugs = {};

my $doc = XMLin($file, keyattr => ['name','content']);
my $artifacts = $doc->{artifacts}->{artifact};

foreach my $tmp (@$artifacts) {
    my $art = $tmp->{field};
    my $id = $art->{artifact_id}->{content};
    my $release = $art->{artifact_group_id}->{content};
    if($art->{status}->{content} =~ /Deleted/) {
        next;
    }
    # if it is closed, and not assigned to a release, then it is 
    # probably old
    if($art->{status}->{content} =~ /Closed/ and
       $release =~ /None/) {
        next;
    }

    if($art->{artifact_type}->{content} =~ /Feature/) {
        $features->{$release}->{$id} = $art;
    } elsif ($art->{artifact_type}->{content} =~ /Bugs/) {
        $bugs->{$release}->{$id} = $art;
    }
}

foreach my $rel (@RELEASES) {
    if(exists $bugs->{$rel} or exists $features->{$rel}) { 
        print "<h2>Release Status for $rel</h2>\n";
        print "<table>\n";

        print "<tr><th colspan=5>Features</th></tr>\n";
        foreach my $bid (sort keys %{$features->{$rel}}) {
            my $bug = $features->{$rel}->{$bid};
            print_item_html($bug);
        }

        print "<tr><th colspan=5>Bugs</th></tr>\n";
        foreach my $bid (sort keys %{$bugs->{$rel}}) {
            my $bug = $bugs->{$rel}->{$bid};
            print_item_html($bug);
        }
        print "</table>\n";
    }
}

sub print_item_html {
    my $art = shift;
    my $bid = $art->{artifact_id}->{content};
    my $status = $art->{status}->{content};
    my $flag = $status;
    my $owner = $art->{assigned_to}->{content};
    my $cat = $art->{category}->{content};
    my $resolve = $art->{resolution}->{content};
    my $sum = $art->{summary}->{content};

    if($owner =~ /nobody/ and $status !~ /Closed/) {
        $flag = "Bad";
    }
    
    my $url = "https://sourceforge.net/tracker/index.php?func=detail&amp;aid=$bid&amp;group_id=71730";
    if($art->{artifact_type}->{content} =~ /Feature/) {
        $url .= "&amp;atid=532254";
    } elsif($art->{artifact_type}->{content} =~ /Bugs/) {
        $url .= "&amp;atid=532251";
    }
    
    print "<tr>\n<td class=$flag>$bid</td>\n";
    print "<td class=sumary><a href=\"$url\">$sum</a></td><td class=owner>$owner</td>\n";
    print "<td class=status>$status - $resolve</td></tr>\n";
    
}
